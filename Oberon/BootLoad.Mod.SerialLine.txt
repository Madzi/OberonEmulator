MODULE* BootLoad;
  IMPORT SYSTEM;
  CONST MT = 12; SP = 14;
    MTOrg = 20H; MemLim = 0E7EF0H; stackOrg = 80000H;
    rsData = -56; rsCtrl = -52;

  PROCEDURE RecInt(VAR x: INTEGER);
    VAR z, y, i: INTEGER;
  BEGIN z := 0;  i := 4;
    REPEAT i := i-1;
      WHILE ~SYSTEM.BIT(rsCtrl, 0) DO SYSTEM.PUT(-64, 7FFFFFFFH) END;
      SYSTEM.GET(rsData, y); z := ROR(z+y, 8)
    UNTIL i = 0;
    x := z
  END RecInt;

  PROCEDURE LoadFromLine;
    VAR len, adr, dat, ml: INTEGER;
  BEGIN RecInt(len);
    SYSTEM.GET(3FFFF80H - 4, ml);
    IF ml = 4D4C696DH THEN
      SYSTEM.GET(3FFFF80H - 8, ml)
    ELSE
      ml := MemLim
    END;
    WHILE len > 0 DO
      RecInt(adr);
      REPEAT RecInt(dat); SYSTEM.PUT(adr, dat); adr := adr + 4; len := len - 4 UNTIL len = 0;
      RecInt(len)
    END;
    SYSTEM.LDREG(11, (ml + 70000H) DIV 100000H * stackOrg);
    SYSTEM.PUT(12, ml); SYSTEM.PUT(24, SYSTEM.REG(11))
  END LoadFromLine;

BEGIN SYSTEM.LDREG(SP, stackOrg); SYSTEM.LDREG(MT, MTOrg); LoadFromLine; LED(84H)
END BootLoad.
